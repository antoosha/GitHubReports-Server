plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.openapi.generator" version "6.0.1"
	id 'java'
}

group = 'cz.cvut.fit.sp1'
version = '0.2.0-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	//spring boot
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//auth jwt
	implementation 'com.auth0:java-jwt:3.19.1'

	//lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	//mapstruct - mappers
	implementation 'org.mapstruct:mapstruct:1.5.2.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

	//github api
	implementation 'org.kohsuke:github-api:1.306'

	//openapi
	implementation 'io.swagger.core.v3:swagger-core:2.2.2'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.0'

	//database
	runtimeOnly 'org.postgresql:postgresql'
	//implementation 'org.flywaydb:flyway-core'

	//tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

springBoot {
	mainClass = 'cz.cvut.fit.sp1.githubreports.GithubReportsApplication'
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/specification.yaml".toString()
	outputDir = "${buildDir}/generated/openapi".toString()
	apiPackage = "org.openapi.api"
	modelPackage = "org.openapi.model"
	configOptions = [
			dateLibrary: "java17",
			interfaceOnly: "true"
	]
}

sourceSets {
	main {
		java {
			srcDir "${buildDir}/generated/openapi/src/main/java"
			srcDir "${rootDir}/src/main/java"
		}
	}
}

bootJar {
	archiveFileName = 'githubreports.jar'
}

jar {
	enabled = false
}

tasks.named('test') {
	useJUnitPlatform()
}

compileJava.dependsOn tasks.openApiGenerate